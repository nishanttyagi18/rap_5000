managed implementation in class zbp_nis_i_student_5000 unique;
strict ( 2 );

define behavior for ZNIS_I_STUDENT_5000 alias Student
persistent table znis_rap_5000
lock master
authorization master ( global, instance )
//etag master <field_name>
{
  create ( authorization : global);
  update;
  delete;

  association _academicres { create; }

  field ( numbering : managed, readonly ) Id;

  action ( features : instance )  setAdmitted result [1] $self;

  validation validateAge on save { create; }

  determination changeDuration on modify { field Course; }

  side effects { field Course affects field Courseduration; }

  mapping for znis_rap_5000
    {
      Id             = id;
      Firstname      = firstname;
      Lastname       = lastname;
      Age            = age;
      Course         = course;
      Courseduration = courseduration;
      Status         = status;
      Gender         = gender;
      Dob            = dob;
    }
}

define behavior for ZNIS_I_AR_5000 alias AcademicResult
lock dependent by _student
authorization dependent by _student
persistent table znis_rap_ar_5000
{
  update;
  delete;
  association _student;

  field ( readonly ) Id, CourseDescription, SemesterDescription, ResultDescription;
  field ( readonly : update ) Course, Semester;

  mapping for znis_rap_ar_5000
    {
      Id        = id;
      Course    = course;
      Semester  = semester;
      Semresult = semresult;
    }
}